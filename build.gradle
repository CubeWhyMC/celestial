plugins {
    id "com.github.johnrengelman.shadow" version "8.+"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id 'org.jetbrains.kotlin.jvm'
}

group = 'org.cubewhy.celestial'
version = '1.6-SNAPSHOT'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
println('Celestial Launcher -> https://www.lunarclient.top/')

compileJava.options.encoding = "UTF-8"


configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url = "https://repo.typesafe.com/typesafe/maven-releases/" }
    mavenCentral()
}

dependencies {
    include 'org.slf4j:slf4j-api:2.0.11'
    include 'org.slf4j:slf4j-log4j12:2.0.11'
    include 'org.apache.logging.log4j:log4j-api:2.22.1'
    include 'org.apache.logging.log4j:log4j-core:2.22.1'

    include 'com.squareup.okhttp3:okhttp:4.12.0'
    include 'com.google.code.gson:gson:2.10.1'
    include 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    include 'com.formdev:flatlaf:3.4-SNAPSHOT'
    include 'co.gongzh.procbridge:procbridge:1.1'
    include 'commons-io:commons-io:2.15.1'
    include 'cn.hutool:hutool-crypto:5.8.25'
    include "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

shadowJar {
    archiveClassifier.set("fatjar")
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude "native-binaries/**"

    exclude "LICENSE.txt"

    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"

    exclude "org/junit/**"
}

jar {
    manifestContentCharset 'utf-8'
    metadataCharset 'utf-8'

    manifest.attributes(
            "Main-Class": "org.cubewhy.celestial.Celestial"
    )
}

kotlin {
    jvmToolchain(17)
}

jar.dependsOn(shadowJar)
