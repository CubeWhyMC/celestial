plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.+"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

group = 'org.cubewhy.celestial'
version = '1.1-pre3-SNAPSHOT'

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
println('Celestial Launcher -> https://www.lunarclient.top/celestial')

compileJava.options.encoding = "UTF-8"

java.sourceCompatibility = 17
java.targetCompatibility = 17

configurations {
    include
    implementation.extendsFrom(include)
}

repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'


    include 'org.slf4j:slf4j-api:2.0.7'
    include 'org.slf4j:slf4j-log4j12:2.0.7'
    include 'org.apache.logging.log4j:log4j-api:2.20.0'
    include 'org.apache.logging.log4j:log4j-core:2.20.0'

    include 'com.squareup.okhttp3:okhttp:4.11.0'
    include 'com.google.code.gson:gson:2.10.1'
    include 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    include 'com.formdev:flatlaf:3.3-SNAPSHOT'
    include 'co.gongzh.procbridge:procbridge:1.1'
    include 'commons-io:commons-io:2.15.1'
    include 'cn.hutool:hutool-crypto:5.8.18'

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveClassifier.set("fatjar")
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude "native-binaries/**"

    exclude "LICENSE.txt"

    exclude "META-INF/maven/**"
    exclude "META-INF/versions/**"

    exclude "org/junit/**"
}

jar {
    manifestContentCharset 'utf-8'
    metadataCharset 'utf-8'

    manifest.attributes(
            "Main-Class": "org.cubewhy.celestial.Celestial"
    )
}

jar.dependsOn(shadowJar)
